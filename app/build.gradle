apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

// Add the Firebase Crashlytics plugin.
apply plugin: 'com.google.firebase.crashlytics'

def versionMajor = 3
def versionMinor = 3
def versionPatch = 0
def versionBuild = 1

def appVersionBuild = Integer.valueOf(System.env.GITHUB_RUN_NUMBER ?: versionBuild)

android {
    namespace 'online.taxcore.pos'

    compileSdkVersion 33

    bundle {
        language {
            enableSplit = false
        }
    }

    defaultConfig {
        applicationId "online.taxcore.pos"

        minSdkVersion 24
        targetSdkVersion 33

        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch * 10 + appVersionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        archivesBaseName = "TaxCore-MobilePOS-$versionCode-v$versionName"

        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file(DEBUG_KEYSTORE_PATH)
            storePassword DEBUG_KEYSTORE_PASS
            keyAlias DEBUG_KEY_ALIAS
            keyPassword DEBUG_KEY_PASS
        }

        /**SIGNING_CONFIG**/
    }

    buildTypes {

        debug {

            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"

            debuggable true
            minifyEnabled false

            signingConfig signingConfigs.debug
        }

        /**BUILD_TYPE**/
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    task printVersionInformation {
        // Full version with build number
        println defaultConfig.versionName
        // Version without build number
        println "${versionMajor}.${versionMinor}.${versionPatch}"
    }

}

ext {
    support = "28.0.0"
    dagger = "2.21"
    permission = "3.1.0"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10"

    implementation 'org.bouncycastle:bcpkix-jdk15on:1.67'

    implementation "com.pawegio.kandroid:kandroid:0.8.7@aar"

    //Android libs
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.security:security-crypto:1.0.0"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"

    implementation "com.android.support:design:$support"
    implementation "com.android.support.constraint:constraint-layout:2.0.4"
    implementation "com.google.android.material:material:1.8.0"

    // dagger
    implementation "com.google.dagger:dagger:$dagger"
    //noinspection OutdatedLibrary
    implementation "com.google.dagger:dagger-android:$dagger"
    implementation "com.google.dagger:dagger-android-support:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$dagger"

    //retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.2"
    implementation "com.google.code.gson:gson:2.8.9"

    //Glide
    implementation "com.github.bumptech.glide:glide:4.12.0"
    kapt "com.github.bumptech.glide:compiler:4.12.0"

    // Material dialogs
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:files:3.3.0'
    implementation 'com.afollestad.material-dialogs:input:3.3.0'
    implementation 'com.afollestad.material-dialogs:bottomsheets:3.3.0'
    implementation 'com.afollestad.material-dialogs:datetime:3.3.0'

    implementation 'com.chaos.view:pinview:1.4.3'

    implementation "org.greenrobot:eventbus:3.0.0"
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'io.github.inflationx:viewpump:2.0.3'
    implementation "com.itextpdf:itextpdf:5.5.13.3"
    implementation "com.madgag:scpkix-jdk15on:1.47.0.1"
    implementation "net.cachapa.expandablelayout:expandablelayout:2.9.2"

    implementation "com.github.vicpinm:krealmextensions:2.5.0"

    // Kotlin
    implementation "org.jetbrains.anko:anko-commons:0.10.5"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    implementation platform('com.google.firebase:firebase-bom:26.2.0')

    // Add the Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    //Permission
    implementation 'com.karumi:dexter:6.2.2'

    implementation "me.dm7.barcodescanner:zxing:1.9.8"
}

apply plugin: 'com.google.gms.google-services'
